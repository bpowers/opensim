kind years (aka year) specializes time
kind months (aka month) specializes year (12 months)
kind days (aka day) specializes month (30 days)

kind Individuals (aka Individual)

kind Rabbits (aka Rabbit, Rab) specializes Individuals
kind Foxes specializes Individuals

model Population (of Individuals) {
	initial_population

	birth_rate (1/time)
	average_lifespan (time)

	flow births := population * birth_rate
	flow deaths := population / avg_lifespan

	stock population := births - deaths, initial_population
}

model RabbitPopulation (of Rabbits) specializes Population {
	carrying_capacity
	consumption_of_rabbits

	initial_population := 500 Rabbits
	birth_rate         := 2 Rabbits/year
	avg_lifespan       := 2 years
	crowding (dmnl)    := population/carrying_capacity

	table effect_on_deaths (dmnl) := [(0, 0.75), (3, 2.5), (6, 6), (8, 11), (10, 20)]

	flow deaths := max(population/avg_lifespan * effect_on_deaths[crowding],
	                   consumption_of_rabbits)
}

model FoxPopulation (of Foxes) specializes Population {
	consumption_of_rabbits (Rabbits)

	initial_population := 30 Foxes
	birth_rate         := .25 Foxes/year
	avg_lifespan       := 4 years
	food_requirements  := 25 Rabbits/year
	food_availability  := consumption_of_rabbits/population/food_requirements

	table mortality_lookup := [(0, 20), (.3, 5), (.5, 2), (1, 1), (2, .5)]

	flow deaths := population/avg_lifespan*mortality_lookup[food_availability]
}

model RabbitFox {
	sim.integration := "euler"

	time := {
		start     := 0 years
		end       := 50 years
		step      := .5 months
		save_step := 1 year
	}

	Table fox_rabbit_consumption_lookup := [(0, 0), (1, 1), (2, 2), (6, 2)]

	fox_consumption_of_rabbits := foxes.population * foxes.food_requirements *
                                     fox_rabbit_consumption_lookup[rabbits.crowding]

	RabbitPopulation rabbits := {
		carrying_capacity := 500 Rabbits
		consumption_of_rabbits := fox_consumption_of_rabbits
	}
	FoxPopulation foxes := {
		consumption_of_rabbits := fox_consumption_of_rabbits
	}
}
